import org.springframework.boot.gradle.tasks.run.BootRun

plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'idea'
}

group = 'ru.memkeeper'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenLocal()
    maven {
        url "https://mavensync.zkoss.org/eval"
    }
    maven {
        url "https://mavensync.zkoss.org/maven2"
    }
    maven { url "http://repo.spring.io/release" }
    maven { url "http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts/" }
    maven { url "http://jasperreports.sourceforge.net/maven2/" }
    mavenCentral()
}

idea {
    module {
        inheritOutputDirs = false
        sourceDirs += files('build/generated/source/annotationProcessor/java/main')
        generatedSourceDirs += files('build/generated/source/annotationProcessor/java/main')
    }
}

dependencies {
    // zk
    implementation 'org.zkoss.zkspringboot:zkspringboot-starter:1.0.4'
    implementation 'org.zkoss.zk:zk:9.0.0-Eval'
    implementation 'org.zkoss.zk:zkmax:9.0.0-Eval'
    implementation 'org.zkoss.zk:zkplus:9.0.0-Eval'
    implementation 'org.zkoss.zk:zuti:9.0.0-Eval'

    // spring boot
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-test'

    // db
    implementation 'org.postgresql:postgresql'
    implementation 'org.liquibase:liquibase-core:3.8.8'
    implementation 'com.h2database:h2:1.4.200'

    // utils
    implementation group: 'com.google.guava', name: 'guava', version: '29.0-jre'

    implementation 'org.immutables:value:2.8.2'
    annotationProcessor 'org.immutables:value:2.8.2'

    implementation "io.springfox:springfox-swagger2:2.9.2"
    implementation "io.springfox:springfox-swagger-ui:2.9.2"

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

task bootRunLocal(type: BootRun) {
    dependsOn(classes)

    doFirst() {
        main = bootJar.mainClassName
        classpath = sourceSets.main.runtimeClasspath
    }
    systemProperty("spring.profiles.active", "local")

    group = "Application"
    description = "Runs this project as a Spring Boot application with local properties"
}

test {
    useJUnitPlatform()
    systemProperty("spring.profiles.active", "test")
}
